:Namespace solutions
    
    ⍝ Every section cover ten problems
    
    ⍝ When the input is a matrix, open the editor with )ED -mat and save
    (⎕IO ⎕ML ⎕PP)←1 1 12
    'factors' 'pco' 'xtimes' ⎕CY 'dfns' ⍝ Import pco, factors
    
    fib←{
        ⍝ https://dfns.dyalog.com/n_fibonacci.htm
        ⍝ p0025
        ⎕ML←3
        ⍵≤2:1
        {(+/∧\⍵='0')↓⍵
        },'ZI9'⎕FMT(⍵-2){
            ⍺>0:(⍺-1)∇(↑⌽⍵)({
                    ∧/⍵<1000000000:⍵ ⋄ (1000000000|⍵)+1⌽⍵≥1000000000
                }(↑⍵)+↑⌽⍵) ⋄ ↑⌽⍵
        }2⍴⊂(-⌈⍵÷40)↑1
    }
    
    :Section First
        f0001 ← {+/⍸∨⌿0=3 5∘.|⍳⍵-1} ⍝ f0001 1000
        t0001 ← +/⍸∘(∨⌿0=3 5∘.|(⍳-∘1))
        
        f0002 ← {+/(⍵⍴0 1 0)/{1∧+∘÷\⍵⍴1}⍵} ⍝ f0002 32
        t0002 ← (1∧(+∘÷\1⍴⍨⊢)) (/⍨) (0 1 0⍴⍨⊢)
        
        t0003 ← ⊃∘⌽factors ⍝ t0003 600851475143
        
        f0004 ← {{⌈/⍵/⍨(⌽≡⊢)¨⍕¨⍵},∘.×⍨⍵} ⍝ p0004 99+⍳900
        t0004 ← ⌈/∘ {⍵/⍨(⌽≡⊢)¨⍕¨⍵}∘ ,(∘.×⍨⊢) ⍝ To tacify {⍵/⍨(⌽≡⊢)¨⍕¨⍵}
        
        f0005 ← {{×/(∪∊⍵)*{⌈/+/¨⍵}¨⍵∘=¨∪∊⍵}factors¨⍳⍵}
        t0005 ← ×/∘(∪∘∊*(⌈/∘(+/¨)⍤1⊢∘∪∘∊∘.=⊢))⊢factors¨⍳
        d0005←{
            fac ← factors¨⍳⍵
            nums ← ∪∊fac
            freq ← {⌈/+/¨⍵}¨fac∘=¨nums ⍝ For each number find the highest frequence in each factors
            ×/nums*freq
        } ⍝ f0005 20
        
        f0006 ← {(×⍨+/⍳⍵)-+/×⍨⍳⍵} ⍝ f0006 100
        t0006 ← ((×⍨+/)-+/⍤×⍨)⍳
        
        f0007 ← pco ⍝ p0007 10001
        
        ⍝ Using the character, cause 10⊥⍣¯1⊢ would give DOMAIN ERROR
        f0008 ← {⌈/13×/⍎∊' ',¨⍵} ⍝ f0008 ∊mat
        t0008 ← ⌈/13×/∘⍎∘∊' '∘,¨
        
        f0009 ← {×/⊃⍵/⍨1000=+/¨⍵} ,{⍵,0.5*⍨+/⍵*2} ¨{(⊂0 0)~⍨,⍵×∘.<⍨⍳⊃⍴⍵} ⍝ f0009 ⍳400 400
        
    :EndSection ⍝ First
    
    :Section Second
        f0010 ← {+/⍸10 pco 1 ⍵} ⍝ f0010 2E6 Solution by https://github.com/rak1507
        t0010 ← +/∘⍸10 pco 1,⊢
        
        d0011←{
            rows←,/mat ⋄ columns←,⌿mat
            dyagonals←{⊃,/(⌽vec)(↑,⍥⊂↓)¨,⌿⍵⌽⍨¯1+vec←⍳≢⍵}mat
            anti_dyagonals←{⊃,/vec(↑,⍥⊂↓)¨,⌿⍵⌽⍨¯1+vec←⌽⍳≢⍵}mat
            ⌈/∊4×/¨rows,columns,{⍵/⍨4≤≢¨⍵}dyagonals,anti_dyagonals
        } ⍝ d0011 mat←↑' '(⍎¨≠⊆⊢)¨↓mat
        
        f0012   ← {{⍵/⍨500≤×/1+↑⊢⌿¨2 pco¨⍵}2÷⍨×⌿0 1∘.+⍵} ⍝ p0012 ⍳12400
        f0012_2 ← {{⍵/⍨500≤{×/1+⊢⌿2 pco ⍵}¨⍵}+/∘⍳¨⍵}
        
        f0013 ← {+/⍎¨↓⍵} ⍝ (10↑~∘'.')⍕ f0013 mat
        t0013 ← +/∘(⍎¨↓)
        
        f0014 ← {⊃⍒{≢r⊣{2|⍵:1+3×⍵ ⋄ ⍵÷2}⍣{1=r,←⍵}⍵⊣r←⍬}¨⍵} ⍝ f0014 ⍳1e6.
        
        p0015 ← {×/(⍳+/⍵)÷∊⍳¨⍵} ⍝ f0015 20 20
        t0015 ← ×/(⍳+/)÷(∊⍳¨)
        
        f0016 ← {+/⊃xtimes/10⊥⍣¯1¨(⍵÷4)⍴2*4} ⍝ f0016 1000 ⍝ 4 cause 1000÷4 and 2*4 are integers
        
        ∇ res ← s0017 ;set1;set2;set3;sum_1000;sum_100_999;sum_10_19;sum_1_9;sum_1_99;sum_20_99
          set1←'one two three four five six seven eight nine'
          set2←'ten eleven twelve thirteen fourteen fifteen sixteen seventeen eighteen nineteen'
          set3←'twenty thirty forty fifty sixty seventy eighty ninety'
          sum_1_9←⍴set1~' '
          sum_10_19←⍴set2~' '
          ⍝ for 20s, 30s, [...] , 80s, 90s: 'twenty twenty-one twenty-two [...] twenty-eight twenty-nine' →
          ⍝ → 10 times 'twenty' + 1 time 'one two three [...] eight nine'
          sum_20_99←(10×⍴set3~' ')+8×sum_1_9
          sum_1_99←sum_1_9+sum_10_19+sum_20_99
          ⍝ For 100s, 200s, [...] , 800s, 900s
          ⍝ ('one hundred' + 99 times 'one hundred and'), ... , ('nine hundred' + 99 times 'nine hundred and') →
          ⍝ → 1 time 'one two three [...] eight nine'   + 9 times 'hundred' +
          ⍝ + 99 times 'one two three [...] eight nine' + 891 times 'hundred and'
          ⍝ → 100 times 'one two three [...] eight nine' + 9 times 'hundred' + 891 times 'hundred and'
          ⍝ For every nine hundreds repeats sum_1_99
          sum_100_999←(100×sum_1_9)+(9×⍴'hundred')+(891×⍴'hundred and'~' ')+9×sum_1_99
          sum_1000←⍴'one thousand'~' '
          res ← sum_1_99+sum_100_999+sum_1000
        ∇ ⍝ s0017

        f0019 ← {⍴⍎{3 2⌷⎕SE.UCMD ⍵} ¨(⊂'Calendar '),∘⍕¨,(⍳12)∘.,⍵} ⍝ f0019 1900+⍳100
        
    :EndSection ⍝ Second
    
    :Section Third
        f0020 ← +/∘⊃(xtimes/10⊥⍣¯1¨⍳) ⍝ f0020 100
        
        f0021 ← {+/∊{⍵/⍨0,2≡/⍵}{⍵[⍋⍵]}{⍵[⍋⍵]}¨{⍵,¨⍳≢⍵}(+/∘∪⊢∨¯1↓⍳)¨⍳⍵} ⍝ p0021 10000
        
        f0024 ← {' '~⍨⍕¯1+1e6⌷pmat ⍵} ⍝ p0024 10 ⍝ 10⊥ might delete first zero
        ⍝ t0024 ← ' '~⍨⍕∘(¯1+1e6⌷pmat)

        f0025 ← {⊃⍵/⍨1000≤≢∘fib¨⍵} ⍝ f0025 4750+⍳50
        
        ⍝ p0027
        ⍝ g ← {⍵/⍨{⍵=⌈/⍵},⍺{a b←⍕¨⍵ ⋄ ⍎'⊃⌽⍸1 pco {(⍵*2)+(',a,'×⍵)+',b,'}¨⍳',⍕⍺}¨⍵}
        ⍝ 350 g 300 g 250 g 200 g 150 g 100 g 50 g ,⍳50 50 
    
        f0028 ← {+/1++\0,4/2×⍳2÷⍨⍵-1} ⍝ p28 1001
        t0028 ← (+/1+(+\0,4/2×∘⍳2÷⍨-∘1))

    :EndSection ⍝ Third

    :Section Fourth
        f0030 ← {+/⍵/⍨{⍵=+/5*⍨10⊥⍣¯1⊢⍵}¨⍵} ⍝ f0030 9↓⍳1e6
    :EndSection ⍝ Fourth
:EndNamespace
